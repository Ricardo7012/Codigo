-- Restore Transaction Logs

/*  This script generates a script to restore transactions logs to a specified database from a specified date/time  
INSTRUCTIONS:  Change the value for @databasename and @startdate per your requirements. 
               Run this script against the database server from which the backups were made.
               The output generated by this script is another script containing the restore commands.
               Run the resulting script against the database server that contains the database to which the transaction
               log backups should be restored. */
SET NOCOUNT ON

DECLARE @databasename VARCHAR(50)
DECLARE @startdate DATETIME

SELECT @databasename = 'PREHRPROCESSING'

SELECT @startdate = '2004/1/21 19:11:00'

USE msdb

SELECT 'print ' + '''' + 'Begin: ' + '''' + ' + convert(varchar(30), getdate(), 20)' + CHAR(10) + 'print ' + '''' + 'Restoring: ' + d.filename + '''' + CHAR(10) + 'RESTORE LOG ' + @databasename + 'CON FROM DISK = ' + CHAR(10) + '''' + d.filename + '''' + CHAR(10) + 'WITH NORECOVERY' + CHAR(10) + 'print ' + '''' + 'End: ' + '''' + ' + convert(varchar(30), getdate(), 20)' + CHAR(10) + 'GO' + CHAR(10) + CHAR(10)
FROM (
	SELECT c.physical_device_name filename
		, b.backup_finish_date sort
	FROM backupfile a
	INNER JOIN backupset b
		ON a.backup_set_id = b.backup_set_id
	INNER JOIN backupmediafamily c
		ON c.media_set_id = b.media_set_id
	WHERE b.database_name = @databasename AND b.type = 'L' AND c.device_type = 2 AND b.backup_finish_date > @startdate
	) d
ORDER BY d.sort

SET NOCOUNT OFF
