-- Build Delete Scripts by Foreign Keys based on Table

DECLARE @TABLE_NAME AS SYSNAME
DECLARE @COLUMN_NAME AS SYSNAME
DECLARE @IDValue AS INT

SET @TABLE_NAME = 'ABSUser'
SET @COLUMN_NAME = 'UserID'
SET @IDValue = 2

DECLARE @sql AS VARCHAR(max);

WITH RELATED_COLUMNS
AS (
	SELECT QUOTENAME(c.TABLE_SCHEMA) + '.' + QUOTENAME(c.TABLE_NAME) AS [OBJECT_NAME],
		c.COLUMN_NAME
	FROM INFORMATION_SCHEMA.COLUMNS AS c WITH (NOLOCK)
	INNER JOIN INFORMATION_SCHEMA.TABLES AS t WITH (NOLOCK)
		ON c.TABLE_CATALOG = t.TABLE_CATALOG
			AND c.TABLE_SCHEMA = t.TABLE_SCHEMA
			AND c.TABLE_NAME = t.TABLE_NAME
			AND t.TABLE_TYPE = 'BASE TABLE'
	INNER JOIN (
		SELECT rc.CONSTRAINT_CATALOG,
			rc.CONSTRAINT_SCHEMA,
			lkc.TABLE_NAME,
			lkc.COLUMN_NAME
		FROM INFORMATION_SCHEMA.REFERENTIAL_CONSTRAINTS rc WITH (NOLOCK)
		INNER JOIN INFORMATION_SCHEMA.KEY_COLUMN_USAGE lkc WITH (NOLOCK)
			ON lkc.CONSTRAINT_CATALOG = rc.CONSTRAINT_CATALOG
				AND lkc.CONSTRAINT_SCHEMA = rc.CONSTRAINT_SCHEMA
				AND lkc.CONSTRAINT_NAME = rc.CONSTRAINT_NAME
		INNER JOIN INFORMATION_SCHEMA.TABLE_CONSTRAINTS tc WITH (NOLOCK)
			ON rc.CONSTRAINT_CATALOG = tc.CONSTRAINT_CATALOG
				AND rc.CONSTRAINT_SCHEMA = tc.CONSTRAINT_SCHEMA
				AND rc.UNIQUE_CONSTRAINT_NAME = tc.CONSTRAINT_NAME
		INNER JOIN INFORMATION_SCHEMA.KEY_COLUMN_USAGE rkc WITH (NOLOCK)
			ON rkc.CONSTRAINT_CATALOG = tc.CONSTRAINT_CATALOG
				AND rkc.CONSTRAINT_SCHEMA = tc.CONSTRAINT_SCHEMA
				AND rkc.CONSTRAINT_NAME = tc.CONSTRAINT_NAME
		WHERE rkc.COLUMN_NAME = @COLUMN_NAME
			AND rkc.TABLE_NAME = @TABLE_NAME
		) AS j
		ON j.CONSTRAINT_CATALOG = c.TABLE_CATALOG
			AND j.CONSTRAINT_SCHEMA = c.TABLE_SCHEMA
			AND j.TABLE_NAME = c.TABLE_NAME
			AND j.COLUMN_NAME = c.COLUMN_NAME
	)
SELECT @sql = COALESCE(@sql, '') + 'DELETE FROM ' + [OBJECT_NAME] + ' WHERE ' + [COLUMN_NAME] + ' = ' + CONVERT(VARCHAR, @IDValue) + CHAR(13) + CHAR(10)
FROM RELATED_COLUMNS

PRINT @sql
