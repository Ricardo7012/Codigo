-- 1 -- https://blogs.msdn.microsoft.com/alwaysonpro/2015/01/07/how-to-add-a-tde-encrypted-database-to-an-availability-group/ 
USE [master]; 
GO

SELECT  *
FROM    sys.symmetric_keys
WHERE   name = '##MS_DatabaseMasterKey##';
GO

SELECT  NEWID();

-- 2 - IF NO KEY CREATE ON EACH NODE 
CREATE MASTER KEY ENCRYPTION BY PASSWORD =
    '4EAC97F9-6C85-4204-83B6-5757D325A557';
GO

-- 3 
USE [master]; 
GO 
SELECT  DB_NAME(database_id) [TDE Encrypted DB Name] ,
        c.name AS CertName ,
        encryptor_thumbprint
FROM    sys.dm_database_encryption_keys dek
        INNER JOIN sys.certificates c ON dek.encryptor_thumbprint = c.thumbprint;
 GO
-- 4 BACKUP THE CERTIFICATE
USE [master];
GO
BACKUP CERTIFICATE QVSQLHSP_MasterCert
    TO FILE = 'E:\MSSQL\Backup\QVSQLHSP_MasterCert.bak'
    WITH PRIVATE KEY (
               FILE = 'E:\MSSQL\Backup\QVSQLHSP_MasterCert.pvk',
               ENCRYPTION BY PASSWORD = '4EAC97F9-6C85-4204-83B6-5757D325A557'); 

-- 5 ON EACH SECONDARY REPLICA INSTANCE, CREATE THE TDE CERTIFICATE FROM THE CERTIFICATE BACKED UP ON THE PRIMARY
GO
USE master
GO
CREATE CERTIFICATE QVSQLHSP_MasterCert 
	FROM FILE = 'E:\MSSQL\Backup\QVSQLHSP_MasterCert.bak' 
	WITH PRIVATE KEY ( 
		FILE = 'E:\MSSQL\Backup\QVSQLHSP_MasterCert.pvk', 
		DECRYPTION BY PASSWORD = '4EAC97F9-6C85-4204-83B6-5757D325A557');
GO
-- 6 ON THE PRIMARY REPLICA INSTANCE (SQL1), CREATE A FULL DATABASE BACKUP OF THE TDE ENCRYPTED DATABASE
USE [master]; 
GO 
BACKUP DATABASE HSP TO DISK = 'E:\MSSQL\Backup\HSP_TDE_full.bak'
	WITH
	FORMAT, 
	COMPRESSION,
	STATS=10;
GO

-- 7 ON THE PRIMARY REPLICA INSTANCE (SQL1), CREATE A TRANSACTION LOG BACKUP OF THE TDE ENCRYPTED DATABASE
USE [master]; 
GO 
BACKUP LOG HSP TO DISK = 'E:\MSSQL\Backup\HSP_TDE_log.trn';
GO

-- 8 ON THE PRIMARY REPLICA INSTANCE (SQL1), ADD THE TDE ENCRYPTED DATABASE TO THE AVAILABILITY GROUP
USE [master]; 
GO 
ALTER AVAILABILITY GROUP [QVHAGHSP01] ADD DATABASE [HSP];
GO

-- 9 ON EACH SECONDARY REPLICA INSTANCE, RESTORE THE FULL BACKUP (FROM STEP FOUR) WITH NO RECOVERY
USE [master]; 
GO 
RESTORE DATABASE HSP FROM DISK = 'E:\MSSQL\Backup\HSP_TDE_full.bak' WITH NORECOVERY, STATS=10;
GO

-- 10 ON EACH SECONDARY REPLICA INSTANCE, RESTORE THE TRANSACTION LOG BACKUP (FROM STEP FIVE) WITH NO RECOVERY
USE [master]; 
GO 
RESTORE LOG HSP FROM DISK = 'E:\MSSQL\Backup\HSP_TDE_log.trn' WITH NORECOVERY, STATS=10;
GO

-- 11 ON EACH SECONDARY REPLICA INSTANCE, JOIN THE DATABASE TO THE AVAILABILITY GROUP
USE [master] 
GO 
ALTER DATABASE HSP SET HADR AVAILABILITY GROUP = [QVHAGHSP01];
GO
